float hash( vec2 p ) {
	float h = dot(p,vec2(127.1,311.7));	
    return fract(sin(h)*43758.5453123);
}

float noise( in vec2 p ) {
    p.y /= iResolution.x / iResolution.y ; // ratio - independent noise
    vec2 i = floor( p );
    vec2 f = fract( p );
    vec2 u = f*f*f*(f*(f*6.-15.)+10.);
    float v = mix( mix( hash( i + vec2(0.,0.) ), 
                     hash( i + vec2(1.,0.) ), u.x),
                mix( hash( i + vec2(0.,1.) ), 
                     hash( i + vec2(1.,1.) ), u.x), u.y);
    return pow(abs(cos(v)),10.);
}

float noiseLayer(vec2 uv) {
    float freq = 10.; // noise base frequency / size
    const int iter = 14; // noise iteration / depth
    float lacunarity = 0.7; // lacunarity: relative "importance" of smaller octaves
    float v = 0.;
    float sum = 0.;
    for(int i = 0; i < iter; i++) {
        float layerUp = 1. / pow(freq,lacunarity);
    	v += noise(uv*freq) * layerUp;
        sum += layerUp;
		freq *= 2.0896;
    }
    v /= sum;
    return v;
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
	vec2 uv = fragCoord.xy / iResolution.xy;
    
    uv.x += noiseLayer(uv*0.1+iTime/800.);    
    uv.y += noiseLayer(uv*0.1+iTime/900.);
    
    float v = noiseLayer(uv-vec2(iMouse.x,iMouse.y)/200.+iTime/800.);
        
    v = v*v*2.;
    
    fragColor = vec4(v);

}

https://www.shadertoy.com/view/lsKXWc